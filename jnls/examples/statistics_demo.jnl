! statistics.jnl - example statistical calculations from FERRET *sh* 11/91
! Description: demo of some sample distribution functions and plots

! minor changes 11/93 for FERRET V3.01
! Updated 1/2023

! some simple manipulations of distribution functions ...

! * * * define and plot a gaussian probability density function * * *
! "pdf" == "probability density function"
! "cdf" == "cumulative  density function"
!     ( the random variable X, normally distributed )

CANCEL REGION   ! in case there's region info left over from previous commands
SET WINDOW/SIZe=1.5
CANCEL MODE logo

! define the mathematical variables
LET pi = 3.14159
LET xbar = 2
LET sigma = 1
LET arg = (x-xbar)/sigma
LET/TITLE="gaussian pdf" norm_pdf = (1./(2*pi)^.5)/sigma * exp(-.5*arg*arg)
LET/TITLE="gaussian cdf" norm_cdf = norm_pdf[X=@IIN]

! define the region for plotting and the resolution of calculations
DEFINE AXIS/X=-10:50:.01 xaxis
DEFINE GRID/X=xaxis g_gauss
SET GRID g_gauss	! abstract variables will use g_gauss by default
SET REGION/X=-2:6


! make a few demo plots:
! plot the bell curve of the Normal pdf

PLOT/COLOR=blue norm_pdf
pause

! plot the integrated bell curve

PLOT/COLOR=blue norm_cdf
pause

! plot both together scaling the PDF so its max is 1

PLOT norm_cdf,norm_pdf/norm_pdf[X=@MAX]
pause


! define and compute some simple statistics
LET median = norm_cdf[X=@LOC:.5]	! where is cdf equal to 0.5 ?
LET wt_pdf = x*norm_pdf
LET mean = wt_pdf[X=@DIN]		! integrate X*pdf
LIST mean,median

pause

! * * * define and plot a LOG-NORMAL probability density function * * *
!    this is done by associating the values from the normal cdf with a
!    transformed axis - using the fact that exp(x) is monotonic

! create an exponentially transformed axis

CANCEL REGION
LET beta = 5	! 0 maps into beta
LET lamda = .5	
LET exp_trns = beta*exp(lamda*x)	! exponentially transformed axis
DEFINE AXIS/X ax_exp = exp_trns

LET lnorm = norm_cdf[gx=ax_exp@asn]

! then normalize it and define the pdf as its derivative

LET lnrm_cdf = lnorm/lnorm[X=@MAX]	! normalize the cdf
LET lnrm_pdf = lnrm_cdf[X=@DDB]

! plot the log-normal cdf and the scaled pdf
SET REGION/X=0:50

PLOT lnrm_cdf,lnrm_pdf/lnrm_pdf[X=@MAX]

! define and compute some simple statistics
LET median = LNRM_CDF[X=@LOC:.5]
LET wt_pdf = X*LNRM_PDF
LET mean = wt_pdf[X=@DIN]	
LIST mean,median
