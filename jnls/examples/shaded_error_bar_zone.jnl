! 7/98 - demonstrate a shaded region (representing a region of uncertainty)
! behind a line plot
! *acm* 7/01 update /ylimits to /vlimits
set mode verify
set window/size=1.5
cancel mode logo

! This example uses a time series. In earlier versions of Ferret this 
! required a time axis definition in hours. This is no longer the case.

! Description: Demo of shaded (error uncertainty) region around line plot

! Define a time series axis
DEFINE AXIS/t=1-jan-1980:1-jan-1981:24/UNIT=hours thr
LET fcn = SIN(T[gt=thr]/500)

! basic plot of function
PLOT/VLIMITS=-2:2 fcn

! 2D version of function
LET fcn_2d = z[z=-2:2:.4] - fcn

! illustrate what the 2D function looks like: positive above, negative below
FILL/PALETTE=centered fcn_2d
pause

! now illustrate a shaded uncertainty region around the time series
FILL/LEV=(-.3)(.3)/PALETTE=gray/NOKEY/TITLE="My Function" fcn_2d
PLOT/OVER/NOLAB fcn

