\cancel mode verify
! ellipse.jnl - draw an ellipse inscribed in the indicated corners
! 8/94

! Description: Overlay an ellipse outline from inscribing rectangle coordinates

! usage:  GO ellipse xlo xhi ylo yhi [color and thickness]
! 	   (arguments identical to GO box)

! Notes:
!  Argument 5 is any COLOR=/THICKNESS or /SYMBOL qualifiers. Default /COLOR=black/THICK=2

! test pen_number before performing any actions
query/ignore $1%<Usage: GO ellipse xlo xhi ylo yhi [color and thickness]%
query/ignore $2%<Usage: GO ellipse xlo xhi ylo yhi [color and thickness]%
query/ignore $3%<Usage: GO ellipse xlo xhi ylo yhi [color and thickness]%
query/ignore $4%<Usage: GO ellipse xlo xhi ylo yhi [color and thickness]%

LET/QUIET xellipse_axis = ($2) - ($1)
LET/QUIET yellipse_axis = ($4) - ($3)
LET/QUIET xellipse_center = (($2)+($1))/2
LET/QUIET yellipse_center = (($4)+($3))/2
LET/QUIET ellipse_theta = i * (6.29/100)   ! just over 2 PI in 100 points
LET/QUIET xellipse = xellipse_axis/2 * COS(ellipse_theta) +  xellipse_center
LET/QUIET yellipse = yellipse_axis/2 * SIN(ellipse_theta) +  yellipse_center

! Define the PLOT command

DEFINE SYMBOL penqual = LINE/COLOR=black/THICK=2
IF ($5"0|*>1") THEN
   DEFINE SYMBOL penqual = ($5)
   IF `STRINDEX("($penqual)", "/") EQ 0` THEN
      DEFINE SYMBOL penqual = /LINE/COLOR=($5)
   ELSE
      IF `STRINDEX("($penqual)", "/SY") EQ 0` THEN \
        DEFINE SYMBOL penqual = /LINE($5)
   ENDIF
ENDIF
PLOT/VS/NOLAB/OVERLAY($penqual)/I=1:101  xellipse, yellipse

! clean up
set mode/last verify
