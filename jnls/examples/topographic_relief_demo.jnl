\set mode verify
cancel mode logo

! relief_demo.jnl *jd* 11/91

! *acm* 7/01 change uppercase GO LAND to GO land

! Description: 
! This demonstration reviews coarse subsets of the ETOPO (Equator to Poles)
! data set available from NOAA's National Geophysical Data Center in Boulder.  

! Note the PyFerret distribution includes
!   etopo120 at 2  degree resolution 
!   etopo60  at 1   degree resolution 
!   etopo20  at 1/3  degree resolution 
!   etopo05  at 1/12 degree resolution

! Advanced use of FERRET commands in the demo will give you helpful examples
! of their use.  See the User's Guide for more on individual commands.

CANCEL REGION
SET DATA etopo60
set window/size=1.5/ASPECT=.86
PPL CROSS 1  ! Delineates the equator when plots are made
SHOW DATA 

MESSAGE

! Let's first look at the relief of the earth's surface at 1 degree resolution

LET/TITLE="Surface relief x1000 (meters)" R1000 ROSE/1000
CONTOUR/LEV=(-8,8,2,-1) R1000

MESSAGE

! Visualizing that in another way...

! Change the color palette from the default.
! include any of the centered palettes, and specific topo palettes. 
! For instancetry these palettes
!
!    topo_blue_brown
!    topo and topo_light 
!
!    dark_land_sea 
!    land_sea
!
!    centered
!    blue_red_centered
!
!   See the comments in topo.spk and topo_light.spk for setting color levels
!   Use centered color levels for percent palettes, including the centered palettes.

palette topo_blue_brown

SHADE/LEVEL=c/palette=topo_blue_brown ROSE
GO land

MESSAGE

! There is another data set at 1/3 degree resolution, 
! Good levels for US elevation (in meters):
! (-6000,-1000,1000)(-1000,-100,100)(-100,100,10)(100,1000,100)(1000,6000,1000)


SET DATA etopo60
SHADE/X=50E:180/Y=-40:40/LEV=\
(-inf)(-6000,-1000,1000)(-1000,-100,100)(-100,100,10)(100,1000,100)(1000,6000,1000)(inf)\
/palette=topo_light ROSE

GO land

MESSAGE

! Sharpening this up with outines --

go land

MESSAGE

! Different PALETTES can be used to change color and emphasis in a shade plot. 
! For example, in the lower plot, the /PALETTE qualifier is used to temporarily 
! change the palette to greyscale.

SET VIEW UPPER
SHADE/X=110W:100E/Y=-20:20/LEV=(-8000,8000,500) ROSE

SET VIEW LOWER
SHADE/X=110W:100E/Y=-20:20/PALETTE=greyscale/LEV=(-8000,0,500) ROSE
go land

! Here

MESSAGE

! The current palette, set above with the PALETTE command, remains in effect 
! if the /PALETTE option is not specified

CAN VIEW
SHADE/X=110W:50E/Y=-50:30/LEV=(-8000,8000,500) ROSE

MESSAGE

! And the CONTOUR/FILL command (with an alias of FILL) can be used to generate
! smooth color filled contours -- but it is generally slower to execute than 
! shade.

FILL/X=110W:50E/Y=-50:30/LEV=(-8000,8000,500)/title="ETOPO 20 1/3 degree dataset" ROSE
CONTOUR/OVER/X=110W:50E/Y=-50:30/LEV=(0) ROSE

MESSAGE

CAN VIEW
palette default
PPL PEN 1 1
PPL CROSS 0
