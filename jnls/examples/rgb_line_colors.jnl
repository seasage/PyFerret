SET GRID abstract
CANCEL VARIABLES/ALL
CANCEL MODE LOGO

SET MODE VERIFY

! rgb_line_colors.jnl 
!

! Description: Demonstrate the use of PLOT/COLOR=(r,g,b) to set the 
!              color of a line plot. This script uses the plots from 
!              the fun spirograph demo. The same /COLOR= settings also 
!              apply to the CONTOUR command
!
! Hint: If there are color(s) in a Ferret palette that you want to use, the
! Unix command Fpalette lists the r-g-b colors from that palette file.
!
! For example, search for a darker green than /COLOR=green would give us:
!
!  > Fpalette green  ! lists all of the palettes with "green" in their name
! 
!  > Fpalette -more green_dark
!  * * * * * * * * in /home/user/miniconda3/envs/FERRET/ppl
!     0    0  55  00 
!   100    0  55  00 
! 
! Also, note that there are a number of single-color palettes which are shown 
! in the script single_color_palettes.jnl
! 
 

IF ($program_name"1|Ferret>1|*>0") THEN
   say "The useage /COLOR=(r,g,b) applies only to PyFerret. Exiting"
   exit/script
ENDIF


set window/size=1.5/aspect=1

! The /COLOR= qualifier can take an argument of a pen number or a named
! color: blue, red, green, lightblue, purple, black

PLOT/VS/COLOR=blue/LINE/I=1:50/J=1:50/title="/COLOR=blue" 6*I/J,5*J/I

MESSAGE


! For PyFerret the color may also be specified with red-green-blue (r,g,b)
! or red-green-blue-opacity (r,g,b,a) specification, where the values are
! between 0 and 100.

LET ODD = MOD(I,2) EQ 0
LET XPT = IF ODD THEN (-1*SIN(I/100)) ELSE SIN(I/100)
LET YPT = IF ODD THEN COS(I/100) ELSE (-1*COS(I/100))
PLOT/VS/COLOR=(0,55,0)/LINE/I=1:314/title="/COLOR=(0,55,0)" XPT,YPT
MESSAGE

! Variation on a theme: place the circumference points 10 times
! further apart but go around the circle 10 times.
LET XPT2 = IF ODD THEN (-1*SIN(I/10)) ELSE SIN(I/10)
LET YPT2 = IF ODD THEN COS(I/10) ELSE (-1*COS(I/10))
PLOT/VS/COLOR=(55,0,55)/LINE/I=1:314/title="/COLOR=(55,0,55)" XPT2,YPT2
MESSAGE

! A bit wilder, still ...
LET XPT3 = IF ODD THEN (-1*SIN(I/10)) ELSE 1/(2+SIN(I/10))
LET YPT3 = IF ODD THEN COS(I/10) ELSE (1/(2+COS(I/10)))
PLOT/VS/COLOR=(100,30,10)/LINE/I=1:314/title="/COLOR=(100,30,10)" XPT3,YPT3
MESSAGE

! Now for some true art ...
! Move the circle center while drawing circumference points.
LET XCNTR = COS(I/30)
LET YCNTR = SIN(I/30)
LET XPT4 = IF ODD THEN (-1*SIN(I/10)) ELSE SIN(I/10)
LET YPT4 = IF ODD THEN COS(I/10) ELSE (-1*COS(I/10))
LET XPT5 = XPT4 + XCNTR
LET YPT5 = YPT4 + YCNTR
PLOT/VS/COLOR=(0,55,55)/LINE/I=1:380/title="/COLOR=(0,55,55)" XPT5,YPT5

message
set window/clear/asp
